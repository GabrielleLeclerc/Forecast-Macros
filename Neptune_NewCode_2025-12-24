Option Explicit

Sub Boxes_Shipped_Forecast()
    Dim wsA As Worksheet: Set wsA = ThisWorkbook.Worksheets("Boxes_Shipped_Assumptions")
    Dim lo As ListObject: Set lo = wsA.ListObjects("WorksheetNames")

    '=== Forecast start date from assumptions ===
    Dim forecastStart As Date
    forecastStart = CDate(wsA.Range("B1").Value)

    '=== Set up / create the Debug sheet and the output row counter ===
    Dim wsDebug As Worksheet
    On Error Resume Next
    Set wsDebug = ThisWorkbook.Worksheets("Boxes_Shipped_DEBUG")
    On Error GoTo 0
    
    If wsDebug Is Nothing Then
        Set wsDebug = ThisWorkbook.Worksheets.Add(After:=ThisWorkbook.Worksheets(ThisWorkbook.Worksheets.Count))
        wsDebug.Name = "Boxes_Shipped_DEBUG"
    End If
    
    wsDebug.Cells.Clear
    wsDebug.Range("A1").Value = "Demand City"
    wsDebug.Range("B1").Value = "Week Start"
    wsDebug.Range("C1").Value = "Week End"
    wsDebug.Range("D1").Value = "Col"

    wsDebug.Columns("B:C").NumberFormat = "yyyy-mm-dd"
    
    Dim outRow As Long
    outRow = 2
    
    Dim c As Range
    For Each c In lo.ListColumns("Worksheet").DataBodyRange.Cells
        Dim Worksheet As String
        Worksheet = Trim(CStr(c.Value))
        If Len(Worksheet) = 0 Then GoTo NextOne
        
        Dim wsT As Worksheet
        On Error Resume Next
        Set wsT = ThisWorkbook.Worksheets(Worksheet)
        On Error GoTo 0
        
        If wsT Is Nothing Then
            Debug.Print "Missing sheet:", Worksheet
        Else
            Debug.Print "Reading:", wsT.Name, "A1=", wsT.Range("A1").Value

            Dim demandCity As String
            demandCity = DemandCityFromSheetName(wsT.Name)
            
            '=== Find the forecast start column on row 2 (match forecastStart) ===
            Dim startCol As Long, lastCol As Long, col As Long
            startCol = 0
            
            lastCol = wsT.Cells(2, wsT.Columns.Count).End(xlToLeft).Column
            
            For col = 1 To lastCol
                If IsDate(wsT.Cells(2, col).Value) Then
                    If DateValue(wsT.Cells(2, col).Value) = DateValue(forecastStart) Then
                        startCol = col
                        Exit For
                    End If
                End If
            Next col
            
            If startCol = 0 Then
                Debug.Print "Forecast start date not found on row 2 for sheet:", wsT.Name, "Start:", Format(forecastStart, "yyyy-mm-dd")
            Else
                '=== Loop week columns from forecastStart onward ===
                For col = startCol To lastCol
                    If Not IsDate(wsT.Cells(2, col).Value) Then Exit For
                    
                    wsDebug.Cells(outRow, "A").Value = demandCity
                    wsDebug.Cells(outRow, "B").Value = wsT.Cells(2, col).Value
                    wsDebug.Cells(outRow, "C").Value = wsT.Cells(4, col).Value
                    wsDebug.Cells(outRow, "D").Value = Split(wsT.Cells(1, col).Address(False, False), "1")(0)
                    
                    outRow = outRow + 1
                Next col
            End If
        End If
        
NextOne:
        Set wsT = Nothing
    Next c
End Sub

'=== City Name Mapping ===
Private Function DemandCityFromSheetName(ByVal sheetName As String) As String
    Select Case sheetName
        Case "Neptune-Montreal":  DemandCityFromSheetName = "Montreal"
        Case "Neptune-Vancouver": DemandCityFromSheetName = "Vancouver"
        Case "Neptune-Toronto":   DemandCityFromSheetName = "Toronto"
        Case "Neptune-Moreno":    DemandCityFromSheetName = "Moreno"
        Case "Neptune-Dayton":    DemandCityFromSheetName = "Dayton"
        Case Else
            DemandCityFromSheetName = "UNKNOWN (" & sheetName & ")"
    End Select
End Function

